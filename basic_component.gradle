apply from: rootProject.file('cc-settings-2.gradle')
//apply from: 'https://raw.githubusercontent.com/luckybilly/CC/master/cc-settings.gradle'
//先加载local.properties文件
Properties localProperties = new Properties()
try {
    def localFile = project.rootProject.file('local.properties')
    if (localFile != null && localFile.exists()) {
        localProperties.load(localFile.newDataInputStream())
    }
} catch (Exception ignored) {
    println("local.properties not found")
}
//runAsApp = ('true' == localProperties.getProperty(project.name))

//模块名称
def componentName = project.name.replaceAll("component_", "")
println(project.name + "--" + project.ext.runAsApp)
android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName
        if (project.ext.runAsApp) {
            applicationId deps.buildApplicationId + componentName
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resourcePrefix componentName + "_"
        resValue "string", componentName + "_module_name", project.name
        signingConfigs {
            config {
                keyAlias localProperties['keyAlias']
                keyPassword localProperties['keyPassword']
                storeFile file(localProperties['storeFile'])
                storePassword localProperties['storePassword']
//            v2SigningEnabled false
            }
        }
        vectorDrawables.useSupportLibrary = true
    }


    buildTypes {
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}


dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api deps.support.v4
    api deps.support.app_compat
    api deps.support.design
    api deps.support.cardview
    api deps.preference.preference
    api deps.constraint_layout
    //Paging的依赖
    api deps.paging
    api deps.kotlin.stdlib
    api deps.retrofit.runtime
    api deps.retrofit.gson
//    api deps.alibaba.fastjson
    api deps.retrofit.rxjava2
    api deps.okhttp_logging_interceptor
    api deps.rx_android
    api deps.rxjava2
    api deps.rxbinding
    testImplementation deps.junit
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core
}